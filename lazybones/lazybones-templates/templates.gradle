buildscript {
    repositories {
        mavenLocal()
        maven {
            url "http://dl.bintray.com/pledbrook/plugins"
        }
    }

    dependencies {
        classpath "uk.co.cacoethes:lazybones-gradle:1.0.2"
    }
}

import uk.co.cacoethes.gradle.lazybones.LazybonesConventions
import uk.co.cacoethes.gradle.tasks.BintrayGenericUpload

apply plugin: "lazybones-templates"

// The test-tmpl template should *not* be published, so we exclude that particular
// task from the build.
gradle.taskGraph.useFilter({ task -> task.name != "publishTemplateTestTmpl" } as Spec)

// Lazy initialisation of Bintray upload tasks so that not all build users need
// to set the repo.* project properties. The properties are only required when
// executing the publish tasks.
gradle.taskGraph.whenReady { graph ->
    if (graph.allTasks.any { it.name.startsWith("publish") }) {
        verifyProperty(project, 'repo.url')
        verifyProperty(project, 'repo.username')
        verifyProperty(project, 'repo.apiKey')

        graph.allTasks.findAll { it instanceof BintrayGenericUpload }.each { task ->
            task.repositoryUrl = project.'repo.url'
            task.username = project.'repo.username'
            task.apiKey = project.'repo.apiKey'
        }
    }
}


void verifyProperty(Project proj, String name) {
    if (!project.hasProperty(name)) {
        throw new GradleException("You must define the project property '$name'")
    }
}
